//sourceCompatibility = 1.8
//targetCompatibility = 1.8

ext {
    apacheCommonsVersion = "3.4"
    hibernateVersion = "5.1.0.Final"
    jacksonVersion = "2.6.2"
    jsonassertVersion = "1.3.0"
    hamcrestVersion = "1.3"
    mockitoVersion = "2.0.53-beta"
    springDataJpaVersion = "1.10.1.RELEASE"
    springVersion = "4.2.6.RELEASE"
    springSecurityVersion = "4.1.0.RELEASE"
    testNgVersion = "6.9.10"
    jacocoToolVersion = "0.7.6.201602180812"
}

buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'http://repo.corp.nexient.com:8081/artifactory/plugins-release'
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.liquibase:liquibase-gradle-plugin:1.2.1"
        classpath "info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: "eclipse"
    apply plugin: "eclipse-wtp"
    apply plugin: "idea"
    apply plugin: 'liquibase'
    apply plugin: "jacoco"

    group = "com.nexient"
    version = "0.0.2-SNAPSHOT"
    description = "OrgChart"

    repositories {
        mavenLocal()
        maven {
            url 'http://repo.corp.nexient.com:8081/artifactory/libs-release-local'
        }
        maven {
            url 'http://repo.corp.nexient.com:8081/artifactory/plugins-release'
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.springframework:spring-webmvc:$springVersion",
                "org.springframework.data:spring-data-jpa:$springDataJpaVersion",
                "com.fasterxml.jackson:jackson-parent:$jacksonVersion",
                "org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec:1.0.1.Final",
                "org.hibernate:hibernate-validator:$hibernateVersion"

        testCompile "org.testng:testng:$testNgVersion",
                "org.springframework:spring-test:$springVersion",
                "org.mockito:mockito-core:$mockitoVersion",
                "org.hamcrest:hamcrest-all:$hamcrestVersion",
                "com.jayway.jsonpath:json-path:2.0.0",
                "com.github.springtestdbunit:spring-test-dbunit:+",
                "org.apache.commons:commons-lang3:$apacheCommonsVersion",
                "org.assertj:assertj-core:3.2.0",
                "org.skyscreamer:jsonassert:$jsonassertVersion",
                "commons-beanutils:commons-beanutils:1.9.2"

    }
    jacoco {
        toolVersion = jacocoToolVersion
    }
    jacocoTestReport {
        reports {
            html.enabled = true
        }
    }
    test {
        // enable TestNG support (default is JUnit)
        useTestNG()
    }
    test.finalizedBy jacocoTestReport

}

task wrapper(type: Wrapper) {
    gradleVersion = "2.13"
}

project("orgchart-api-data") {
    apply plugin: "info.solidsoft.pitest"

    dependencies {
        compile "org.springframework.data:spring-data-jpa:$springDataJpaVersion",
                "org.springframework.security:spring-security-web:$springSecurityVersion",
                "org.apache.commons:commons-lang3:$apacheCommonsVersion",
                "org.hibernate:hibernate-entitymanager:$hibernateVersion",
                "c3p0:c3p0:0.9.0.4",
                "mysql:mysql-connector-java:5.1.36"

        testCompile "org.hsqldb:hsqldb:2.3.3",
                "org.testng:testng:$testNgVersion"
    }

    pitest{
        targetClasses =["com.nexient.orgchart.data.entity.*",
                "com.nexient.orgchart.data.repository.*"
        ]
    }

}
project("orgchart-api-model") {
    apply plugin: "info.solidsoft.pitest"

    dependencies {
        compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    }

    pitest {
        targetClasses = ["com.nexient.orgchart.model.*"]
    }

}
project("orgchart-api-service") {
    apply plugin: "info.solidsoft.pitest"

    dependencies {
        compile project(":orgchart-api-data"),
                project(":orgchart-api-model"),
                "org.springframework:spring-webmvc:$springVersion"

        testCompile project(":orgchart-api-data").sourceSets.test.output
        testCompile project(":orgchart-api-model").sourceSets.test.output
    }
    pitest {
        targetClasses = ["com.nexient.orgchart.service.*"
        ]
    }
}
project("orgchart-api-web") {
    apply plugin: "info.solidsoft.pitest"

    apply plugin: 'war'

    ext.jacksonVersion = "2.6.0"

    dependencies {
        compile project(':orgchart-api-service')
        compile "javax.servlet:javax.servlet-api:3.0.1",
                "org.springframework.security:spring-security-web:$springSecurityVersion",
                "org.springframework.security:spring-security-config:$springSecurityVersion",
                "io.springfox:springfox-swagger2:2.2.2",
                "io.springfox:springfox-swagger-ui:2.2.2",
                "org.springframework:spring-webmvc:$springVersion",
                "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                "io.springfox:springfox-swagger-ui:2.2.2"

        testCompile project(":orgchart-api-data").sourceSets.test.output
        testCompile project(":orgchart-api-model").sourceSets.test.output

        providedCompile "javax.servlet:javax.servlet-api:3.0.1"
    }

    pitest {
        targetClasses = ["com.nexient.orgchart.web.*"
        ]
    }
    war {
        archiveName = 'api#orgchart.war'
    }
}
/*
    compile group: "org.springframework.security", name: "spring-security-config", version:"3.1.0.RELEASE"
    compile group: "org.springframework.security", name: "spring-security-taglibs", version:"3.1.0.RELEASE"
    compile group: "cglib", name: "cglib", version:"2.2"
    compile group: "javassist", name: "javassist", version:"3.12.1.GA"
    compile group: "antlr", name: "antlr", version:"2.7.6"
    compile group: "commons-collections", name: "commons-collections", version:"3.1"
    compile group: "org.apache.tiles", name: "tiles-jsp", version:"2.2.2"
    compile group: "org.slf4j", name: "slf4j-api", version:"1.7.12"
    compile group: "org.slf4j", name: "slf4j-log4j12", version:"1.7.12"
    compile group: "log4j", name: "log4j", version:"1.2.14"
    compile group: "org.slf4j", name: "jcl-over-slf4j", version:"1.7.12"
    compile group: "dom4j", name: "dom4j", version:"1.6.1"
    compile group: "javax.servlet.jsp.jstl", name: "javax.servlet.jsp.jstl-api", version:"1.2.1"
    compile group: "taglibs", name: "standard", version:"1.1.2"
    compile group: "org.springframework", name: "spring-orm", version:"3.1.0.RELEASE"
    compile group: "org.hibernate", name: "hibernate-annotations", version:"3.5.6-Final"
    compile group: "org.hibernate", name: "hibernate-entitymanager", version:"4.0.0.Final"
    compile group: "org.hibernate", name: "hibernate-validator", version:"4.2.0.Final"
    compile group: "mysql", name: "mysql-connector-java", version:"5.1.36"
 */